import org.gradle.api.internal.artifacts.publish.DefaultPublishArtifact

yeti_project_root = "$project.projectDir"

subprojects {
    apply plugin: 'java'
    group = 'yeti.lang'
    repositories {
        mavenRepo urls: "http://jmockit.googlecode.com/svn/maven-repo"
        mavenCentral()
    }
    dependencies {
        testCompile 'junit:junit:4.10',
                    'mockit:jmockit:0.999.10'
    }
}

project(':asm') {
    asmJarDependencyVersion =    '3.1'
    asmModuleName              =    "asm"
    version                 =    "${asmJarDependencyVersion}r"

    asmJarDir  =            "$project.gradle.gradleUserHomeDir/cache/~mzz/asm/jars"
    asmJarName =            "${asmModuleName}-${asmJarDependencyVersion}.jar"
    asmRenamedJar =         "$buildDir/libs/${asmModuleName}-${version}.jar"

    repositories {
        add(new org.apache.ivy.plugins.resolver.URLResolver()) {
                    name = "Madis' ASM jar location"
                addArtifactPattern 'http://linux.ee/[organisation]/[module]-[revision].[ext]'
        }
    }

    dependencies {
        compile     group: '~mzz', name: 'asm', version: asmJarDependencyVersion
    }

    task downloadAsm {
        mkdir asmJarDir
        ant.get(src: "http://linux.ee/~mzz/$asmJarName", dest: asmJarDir, skipexisting: true, verbose: true)
    }

    compileJava.dependsOn('downloadAsm')

    jar {
        baseName = 'asm-renamer'
    }

    task renameAsmJar(dependsOn: 'compileJava') {
        description = "Overwrites the standard jar task with one that renames the class files in the asm jar."
    }
    renameAsmJar.doLast {
        mkdir "$buildDir/libs"
        ant.java(classname: "yeti.lang.Rename", failonerror: true) {
            classpath(path: "$project.buildDir/classes/main")
            arg(value: "$asmJarDir/$asmJarName")
            arg(value: asmRenamedJar)
        }
    }

    // DefaultPublishArtifact(String name, String extension, String type,
    //                        String classifier, Date date, File file, Object... tasks)
    artifacts {
        archives new DefaultPublishArtifact("asm-3.1r", "jar", "jar", "", new Date(), file(asmRenamedJar), renameAsmJar)
    }
}

project(':special') {
    dependencies {
        compile project(':asm')
    }
}

project(':lang') {
    configurations {
        classGeneratorClasspath
    }
    dependencies {
        compile project(':special')
        classGeneratorClasspath project(':special'), project(':asm')
    }

    task generateClasses(dependsOn: ':special:jar') {
        description = "Generates the Compose, Fun2, and Unsafe classes."
    }
    generateClasses.doLast {
        mkdir "$buildDir/classes/special"
        ant.java(classname: "yeti.lang.SpecialLib", failonerror: true) {
            classpath(path: configurations.classGeneratorClasspath.asPath)
            arg(value: "pre")
            arg(value: "$buildDir/classes/main")
        }
    }

    compileJava.dependsOn('generateClasses')

    task modifyLlistClass(dependsOn: 'compileJava') {
        description = "Modifies the LList class with the LList adapter."
    }
    modifyLlistClass.doLast {
        ant.java(classname: "yeti.lang.SpecialLib", failonerror: true) {
            classpath(path: configurations.classGeneratorClasspath.asPath)
            arg(value: "tr")
            arg(value: "$buildDir/classes/main")
        }
    }

    jar.dependsOn('modifyLlistClass')
}

project(':compiler') {
    dependencies {
        compile project(':asm')
        compile project(':special')
        compile project(':lang')
     }
}

// IntelliJ IDEA configuration sync
/*
task intellijSync {
    description = "Adds gradle dependencies to IntelliJ project library"

    final def librariesDir = new File("$yeti_project_root/.idea${File.separator}libraries")
    librariesDir.mkdirs()

    final def userHomeGradle = project.gradle.gradleUserHomeDir
    println """If you haven't done so already, in the IntelliJ Path Variable Preferences,
                set the USER_HOME_GRADLE variable to '$userHomeGradle.path'"""

    def makeJarList = { path ->
        path.split(File.pathSeparator).collect {
            it.replaceAll userHomeGradle.path, "\\\$USER_HOME_GRADLE\\\$"
        }.collect {
            it.replaceAll yeti_project_root, "\\\$PROJECT_DIR\\\$"
        }
    }
    final def compileJars = makeJarList(configurations.compile.asPath)
    final def testJars = makeJarList(configurations.testCompile.asPath) - compileJars

    def createLibrary = { fileName, libraryName, jars ->
        final def gradleLibXml = new File(librariesDir, fileName)
        gradleLibXml.write """
<component name="libraryTable">
  <library name="$libraryName"/>
</component>"""
        final def xmlRoot = new XmlParser().parse(gradleLibXml)
        final def classesNode = xmlRoot.library[0].appendNode('CLASSES')

        jars.each { jar ->
            classesNode.appendNode('root', [url: "jar://$jar!/"])
        }

        def writer = new StringWriter()
        new XmlNodePrinter(new PrintWriter(writer)).print(xmlRoot)
        gradleLibXml.write writer.toString()
        println "File '${gradleLibXml.path}' updated"
    }
    createLibrary 'Gradle_Libraries.xml', 'Gradle Libraries', compileJars
    createLibrary 'Gradle_Test_Libraries.xml', 'Gradle Test Libraries', testJars
}
*/